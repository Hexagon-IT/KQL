///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Alert tables
// Case1 - Daily alert & severity for MDE/AV 
// Case2 - Org based MITRE ATT&CK technique for past 30 days
// Case3 - Org based high/medium alerts for MDE/AV/MDO/MDI/MDA/XDR
// Email & collaboration tables
// Case1 - Daily mail detection by phishing activity for past 30 days
// Case2 - Daily mail detection by spam activity for past 30 days
// Case3 - Daily mail detection by malware activity past 30 days
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////


//Alert tables - Case1
AlertInfo
| where Timestamp > ago(30d)
| where ServiceSource == "Microsoft Defender for Endpoint"
| summarize AlertNum = count() by Severity, bin(Timestamp, 1d)
| render timechart 


//Alert tables - Case2
AlertInfo
| where Timestamp > ago(30d)
| where ServiceSource in ("Microsoft Defender for Endpoint", "Microsoft Defender for Office 365", "Microsoft Defender for Identity", "Microsoft Defender for Cloud Apps", "Microsoft 365 Defender")
| where isnotempty(AttackTechniques)
| mv-expand DetailedAttackTechniques= parse_json(AttackTechniques)
| summarize MITRE_ATTACK_list = count() by tostring(DetailedAttackTechniques)
| render columnchart


//Alert tables - Case3
AlertInfo
| where Timestamp > ago(30d)
| where Severity in ("Medium", "High")
| summarize 
    Endpoints = countif(ServiceSource == "Microsoft Defender for Endpoint"),
    Identities = countif(ServiceSource == "Microsoft Defender for Identity" or ServiceSource == "AAD Identity Protection"),
    Emails = countif(ServiceSource == "Microsoft Defender for Office 365"),
    Applications = countif(ServiceSource == "Microsoft Cloud App Security"),
    M365D_XDR = countif(ServiceSource == "Microsoft 365 Defender")
    by bin(Timestamp, 1d)
| render timechart 


//Email & collaboration tables - Case1
EmailEvents
| where Timestamp > ago(30d)
| where isnotempty(ThreatTypes)
| where ThreatTypes has "Phish"
| summarize count() by ThreatTypes, bin(Timestamp, 1d)
| render linechart 


//Email & collaboration tables - Case2
EmailEvents
| where Timestamp > ago(30d)
| where isnotempty(ThreatTypes)
| where ThreatTypes has "Spam"
| summarize count() by ThreatTypes, bin(Timestamp, 1d)
| render linechart 


//Email & collaboration tables - Case3
EmailEvents
| where Timestamp > ago(30d)
| where isnotempty(ThreatTypes)
| where ThreatTypes has "Malware"
| summarize count() by ThreatTypes, bin(Timestamp, 1d)
| render linechart 
