# Malware distributor Storm-0324 facilitates ransomware access

Microsoft is tracking a financially motivated threat actor known as Storm-0324, also identified as DEV-0324, TA543, and Sagrid. This group specializes in distributing payloads for other attackers through phishing and exploit kit vectors. Notably, in July 2023, Storm-0324 was observed using an open-source tool to distribute payloads via phishing lures in Microsoft Teams chats, distinct from the Midnight Blizzard campaigns observed earlier in May 2023. Storm-0324 employs highly evasive infection chains, focusing on payment and invoice lures, and is associated with distributing the JSSLoader malware used by the ransomware-as-a-service actor Sangria Tempest (ELBRUS, Carbon Spider, FIN7). Microsoft advises using Microsoft 365 Defender to detect Storm-0324 activity and implementing measures such as the principle of least privilege and credential hygiene to limit the impact of these attacks. The blog provides a detailed analysis of Storm-0324's tools, tactics, and procedures observed in past and recent campaigns.
> ðŸ‘‰ September 12, 2023, [Malware distributor Storm-0324 facilitates ransomware access](https://www.microsoft.com/en-us/security/blog/2023/09/12/malware-distributor-storm-0324-facilitates-ransomware-access/)

## Advanced hunting queries
**Possible TeamsPhisher downloads** 

The following query looks for downloaded files that were potentially facilitated by use of the TeamsPhisher tool. 
Defenders should customize the SharePoint domain name (â€˜mysharepointnameâ€™) in the query.

```kusto
let allowedSharepointDomain = pack_array(
'mysharepointname' //customize Sharepoint domain name and add more domains as needed for your query
);
//
let executable = pack_array(
'exe',
'dll',
'xll',
'msi',
'application'
);
let script = pack_array(
'ps1',
'py',
'vbs',
'bat'
);
let compressed = pack_array(
'rar',
'7z',
'zip',
'tar',
'gz'
);
//
let startTime = ago(1d);
let endTime = now();
DeviceFileEvents
| where Timestamp between (startTime..endTime)
| where ActionType =~ 'FileCreated'
| where InitiatingProcessFileName has 'teams.exe'
    or InitiatingProcessParentFileName has 'teams.exe'
| where InitiatingProcessFileName !has 'update.exe'
    and InitiatingProcessParentFileName !has 'update.exe'
| where FileOriginUrl has 'sharepoint'
    and FileOriginReferrerUrl has_any ('sharepoint', 'teams.microsoft')
| extend fileExt = tolower(tostring(split(FileName,'.')[-1]))
| where fileExt in (executable)
    or fileExt in (script)
    or fileExt in (compressed)
| extend fileGroup = iff( fileExt in (executable),'executable','')
| extend fileGroup = iff( fileExt in (script),'script',fileGroup)
| extend fileGroup = iff( fileExt in (compressed),'compressed',fileGroup)
//
| extend sharePoint_domain = tostring(split(FileOriginUrl,'/')[2])
| where not (sharePoint_domain has_any (allowedSharepointDomain))
| project-reorder Timestamp, DeviceId, DeviceName, sharePoint_domain, FileName, FolderPath, SHA256, FileOriginUrl, FileOriginReferrerUrl
```

## Microsoft Security Blog
September 12, 2023, [Malware distributor Storm-0324 facilitates ransomware access](https://www.microsoft.com/en-us/security/blog/2023/09/12/malware-distributor-storm-0324-facilitates-ransomware-access/)

#### Disclaimer
The views and opinions expressed herein are those of the author and do not necessarily reflect the views of company.
